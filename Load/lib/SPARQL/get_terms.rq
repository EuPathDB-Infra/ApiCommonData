PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

SELECT * {
  {
    SELECT distinct ?sid ?uri ?name ?def
      WHERE { 
        ?entity a owl:Class .
          ?entity rdfs:label ?_label. 
          ?entity obo:IAO_0000115 ?definition .
          BIND (str(?entity) AS ?uri) .
          BIND(REPLACE(?uri, str(obo:), "") AS ?sid) .
          BIND (str(?_label) AS ?name) .
          BIND (str(?definition) AS ?def) .
      }
  }
  UNION
  {
    SELECT distinct ?sid ?uri ?name
      WHERE { 
        ?entity a owl:Class .
          ?entity rdfs:label ?_label. 
          filter not exists { 
            ?entity obo:IAO_0000115 ?definition .
          }
          BIND (str(?entity) AS ?uri) .
          BIND(REPLACE(?uri, str(obo:), "") AS ?sid) .
          BIND (str(?_label) AS ?name) .
      }
  }
  UNION
  {
    SELECT distinct ?sid ?uri ?name
      WHERE { 
        ?thing obo:EUPATH_0001009 ?_unitIRI .
          ?thing obo:EUPATH_0001008 ?_unitLabel .
          BIND(str(?_unitIRI) AS ?uri) .
          BIND(str(?_unitLabel) AS ?name) .
          BIND(REPLACE(?uri, str(obo:), "") AS ?sid) .
      }
  }
}
