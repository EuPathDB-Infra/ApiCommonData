# Get term IRI with its label, display order and parent
# It can be used to add/update display order of terms in the search filters

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT distinct ?entity ?label ?displayOrder ?plabel
WHERE { 
	?entity a owl:Class .
	?entity rdfs:label ?tlabel .
    	FILTER not exists { 
      		?entity owl:deprecated ?dep .
    	}
	BIND (str(?tlabel) AS ?label) .
	OPTIONAL {
		?entity rdfs:subClassOf ?parent .
		?parent rdfs:label ?parentlabel . 
		BIND (str(?parentlabel) AS ?plabel) .
	}
	OPTIONAL {
		?entity obo:EUPATH_0000274 ?order .
		BIND (xsd:integer(?order) AS ?displayOrder) .
	}
}
ORDER BY ASC (?plabel) ASC (?displayOrder) ASC (?label)