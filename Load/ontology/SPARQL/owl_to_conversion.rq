PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

SELECT distinct ?IRI 
  (GROUP_CONCAT(DISTINCT ?slabel;separator=" | ") AS ?label)
  (GROUP_CONCAT(DISTINCT ?dOrder;separator=" | ") AS ?displayOrder)
  (GROUP_CONCAT(DISTINCT ?var;separator=" | ") AS ?variable)

  (GROUP_CONCAT(DISTINCT ?dFile;separator=" | ") AS ?dataFile)
  (GROUP_CONCAT(DISTINCT ?def;separator=" | ") AS ?definition)
  (GROUP_CONCAT(DISTINCT ?cate;separator=" | ") AS ?category)
  (GROUP_CONCAT(DISTINCT ?pLabel;separator=" | ") AS ?parentLabel)
  (GROUP_CONCAT(DISTINCT ?pIRI;separator=" | ") AS ?parentIRI)
  (GROUP_CONCAT(DISTINCT ?cDescription;separator=" | ") AS ?codebookDescription)
  (GROUP_CONCAT(DISTINCT ?cValues;separator=" | ") AS ?codebookValues)
  (GROUP_CONCAT(DISTINCT ?tType;separator=" | ") AS ?termType)
WHERE { 
	?IRI a owl:Class .
	?IRI rdfs:label ?tlabel .
    	FILTER not exists { 
      		?IRI owl:deprecated ?dep .
    	}
	BIND (str(?tlabel) AS ?slabel) .
	OPTIONAL {
		?IRI obo:EUPATH_0000274 ?dOrder .
    	}
     	OPTIONAL {
        	?IRI obo:EUPATH_0000755 ?var .
    	} 

    	OPTIONAL {
        	?IRI obo:EUPATH_0001001 ?dFile .
    	}
    	OPTIONAL {
        	?IRI obo:IAO_0000115 ?def .
    	}
    	OPTIONAL {
        	?IRI obo:EUPATH_0001002 ?cate .
    	}
	OPTIONAL {
		?IRI rdfs:subClassOf ?pIRI .
	}
	OPTIONAL {
		?pIRI rdfs:label ?palabel . 
		BIND (str(?palabel) AS ?pLabel) .
	}
    	OPTIONAL {
        	?IRI obo:EUPATH_0001003 ?cDescription .
    	}
    	OPTIONAL {
        	?IRI obo:EUPATH_0001004 ?cValues .
    	}
    	OPTIONAL {
        	?IRI obo:EUPATH_0001005 ?tType .
    	}
}
GROUP BY ?IRI 
ORDER BY ASC(?category) ASC(?parentLabel) ASC(?label)
