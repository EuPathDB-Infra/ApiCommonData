# Check any leaf terms without corresponding variables in the dataset

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

SELECT DISTINCT ?sid ?label ?multifilter ?parent_entity ?parent_label ?project
WHERE {
  <http://purl.obolibrary.org/obo/projects.owl> owl:imports ?file .
  GRAPH ?file {?entity rdfs:label ?org_label .
	OPTIONAL {
	 ?entity rdfs:subClassOf ?parent_entity . 
  	 ?parent_entity rdfs:label ?plabel .
	}
	OPTIONAL {
	 ?entity obo:EUPATH_0001005 ?multifilter .
	}
    } .
  ?entity a owl:Class .
  filter exists { 
	?entity obo:EUPATH_0001005 ?multifilter .
  }
  BIND(STRAFTER(STR(?file), "/obo/") AS ?f) . 
  BIND(IF(STRENDS(?f, ".owl"), STRBEFORE(?f, ".owl"), ?f) AS ?f2) .
  BIND(COALESCE(
	IF(STRSTARTS(?f2, "gates/"), STRAFTER(?f2, "gates/"), ?c), 
	IF(CONTAINS(?f2, "icemr/"), STRAFTER(?f2, "icemr/"), ?c), 
	IF(STRSTARTS(?f2, "eupath/"), STRAFTER(?f2, "eupath/"), ?c), 
	?f2) AS ?project) .
  BIND(COALESCE(
 	IF(CONTAINS(STR(?entity), "/obo/"), STRAFTER(STR(?entity), "/obo/"), ?c), 
	IF(CONTAINS(STR(?entity), "/efo/"), STRAFTER(STR(?entity), "/efo/"), ?c),
 	IF(CONTAINS(STR(?entity), "#"), STRAFTER(STR(?entity), "#"), ?c),
  	REPLACE(STRAFTER(STR(?entity), "/ontology/"), "/", "_")) AS ?sid) .
  BIND (str(?org_label) AS ?label) .
  BIND (str(?plabel) AS ?parent_label)
}
ORDER BY ASC(?sid)
