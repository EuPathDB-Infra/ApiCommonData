# Find terms with more than one parent

PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?sid
  (COUNT(DISTINCT ?parent_entity) AS ?pid_count)
  (GROUP_CONCAT(DISTINCT ?label;separator="|") AS ?labels)
  (GROUP_CONCAT(DISTINCT ?plabel;separator="|") AS ?pLabels)
WHERE {
  ?entity rdfs:label ?org_label .
  ?entity rdfs:subClassOf ?parent_entity .
  ?parent_entity rdfs:label ?parent_label .
  BIND (str(?org_label) AS ?label)
  BIND (str(?parent_label) AS ?plabel)
  BIND(COALESCE(
	IF(CONTAINS(STR(?entity), "/obo/"), STRAFTER(STR(?entity), "/obo/"), ?c), 
	IF(CONTAINS(STR(?entity), "/efo/"), STRAFTER(STR(?entity), "/efo/"), ?c),
 	REPLACE(STRAFTER(STR(?entity), "/ontology/"), "/", "_")) AS ?sid)
}
GROUP BY ?sid
HAVING (?pid_count > 1)
ORDER BY DESC(?label)