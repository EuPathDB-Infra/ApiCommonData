#!/usr/bin/perl -w

use strict;
use lib "$ENV{GUS_HOME}/lib/perl";


use DBI;
use List::Util qw(min max);
use Getopt::Long qw(GetOptions);
use GUS::Supported::GusConfig;
use GUS::ObjRelP::DbiDatabase;

my ($inputfile, $algInvId, $debug);
my $gusConfigFile = "$ENV{GUS_HOME}/config/gus.config";

GetOptions("inputfile=s" => \$inputfile,
	   "algInvId=i" => \$algInvId,
	   "gusConfigFile=s" => \$gusConfigFile,
	   "debug" => \$debug
	  );

unless ($algInvId && -e $inputfile) {
  die
"
Validate the gene models in a database.

Usage: validateISF --inputfile bioperlTreeFile --algInvId id [--gusConfigFile file] [--debug]

";
}

my $answers = &_getExpectedAnswers($inputfile, $debug);

my $gusconfig = GUS::Supported::GusConfig->new($gusConfigFile);

my $db = GUS::ObjRelP::DbiDatabase->new($gusconfig->getDbiDsn(),
                                        $gusconfig->getDatabaseLogin(),
                                        $gusconfig->getDatabasePassword(),
                                        $debug,0,1,
                                        $gusconfig->getCoreSchemaName()
				       );
my $dbh = $db->getQueryHandle();


my $queries = &_makeTestSql($algInvId, $debug);

&_validateData($queries, $answers, $dbh, $debug);


############################################################################

sub _getExpectedAnswers{
  my ($file, $debug) = @_;
  open(FILE, $file) || die "Couldn't open input file '$file' for reading\n";


  my $currentFeature;
  my %answers;
  while (<FILE>) {
    if (/^\s*<\s(\w+)\s>/) {
      $currentFeature = "f:$1";
      next if ($currentFeature eq "f:gene");
      if (!$answers{$currentFeature}) {
	$answers{$currentFeature} = 1;
      }else{
	$answers{$currentFeature}++;
      }
    } elsif (/^\s*(\w+):/) {
      my $qualifier = "q:$1";
      if (!$answers{$qualifier}) {
	$answers{$qualifier} = 1;
      }else{
	$answers{$qualifier}++;
      }
    }
  }

  $answers{'test:coding_genes'} = _nonNull(\%answers, 'f:coding_gene');
  $answers{'test:rRNAs'} = _nonNull(\%answers, 'f:rRNA_gene');
  $answers{'test:tRNAs'} = _nonNull(\%answers, 'f:tRNA_gene');
  $answers{'test:snRNAs'} = _nonNull(\%answers, 'f:snRNA_gene');
  $answers{'test:pseudo_genes'} = $answers{'q:pseudo'};
  $answers{'test:unique_transcript_parents'} =
    _nonNull(\%answers, 'f:coding_gene') + _nonNull(\%answers, 'f:rRNA_gene') +
      _nonNull(\%answers, 'f:tRNA_gene') + _nonNull(\%answers, 'f:snRNA_gene');
  $answers{'test:exons'} = _nonNull(\%answers, 'f:exon');
  $answers{'test:unique_exon_parents'} =
    $answers{'test:unique_transcript_parents'};
  $answers{'test:aa_seqs'} = $answers{'test:coding_genes'};
  $answers{'test:spliced_seqs'} = _nonNull(\%answers, 'f:transcript');
  $answers{'test:exon_rna_feature'} = _nonNull(\%answers, 'f:exon');


  if ($debug){
    foreach my $key (sort(keys %answers)) {
      print STDERR "$key: ".$answers{$key}."\n";
    }
  }

  return \%answers;

}

sub _nonNull {
  my ($hash, $key) = @_;
  return $hash->{$key}? $hash->{$key} : 0;
}

sub _makeTestSql{
  my ($algInvId, $debug) = @_;
  my %queries;

  print STDERR "Preparing SQL using AlgInvId: $algInvId\n" if $debug;

  $queries{'test:coding_genes'} = <<EOSQL;
SELECT count(f.na_feature_id)
FROM DoTs.GeneFeature f,
     SRes.SequenceOntology s
WHERE f.row_alg_invocation_id  = $algInvId
AND f.sequence_ontology_id = s.sequence_ontology_id
AND s.term_name IN ('protein_coding', 'pseudogene')
EOSQL

  $queries{'test:rRNAs'} = <<EOSQL;
SELECT count(f.na_feature_id)
FROM DoTs.GeneFeature f,
     SRes.SequenceOntology s
WHERE f.row_alg_invocation_id  = $algInvId
AND f.sequence_ontology_id = s.sequence_ontology_id
AND s.term_name = 'rRNA'
EOSQL

  $queries{'test:tRNAs'} = <<EOSQL;
SELECT count(f.na_feature_id)
FROM DoTs.GeneFeature f,
     SRes.SequenceOntology s
WHERE f.row_alg_invocation_id  = $algInvId
AND f.sequence_ontology_id = s.sequence_ontology_id
AND s.term_name = 'tRNA'
EOSQL

  $queries{'test:snRNAs'} = <<EOSQL;
SELECT count(f.na_feature_id)
FROM DoTs.GeneFeature f,
     SRes.SequenceOntology s
WHERE f.row_alg_invocation_id  = $algInvId
AND f.sequence_ontology_id = s.sequence_ontology_id
AND s.term_name = 'snRNA'
EOSQL

  $queries{'test:pseudo_genes'} = <<EOSQL;
SELECT count(f.na_feature_id)
FROM DoTs.GeneFeature f,
     SRes.SequenceOntology s
WHERE f.row_alg_invocation_id  = $algInvId
AND f.sequence_ontology_id = s.sequence_ontology_id
AND s.term_name = 'pseudogene'
EOSQL

  # count transcripts (that are attached to genes)
  $queries{'test:unique_transcript_parents'} = <<EOSQL;
SELECT count (distinct t.parent_id)
FROM DoTs.Transcript t,
     DoTS.GeneFeature g
WHERE g.row_alg_invocation_id  = $algInvId
AND g.na_feature_id = t.parent_id
EOSQL

  $queries{'test:exons'} = <<EOSQL;
SELECT count(e.na_feature_id)
FROM DoTs.ExonFeature e,
     DoTS.GeneFeature g
WHERE g.row_alg_invocation_id  = $algInvId
AND g.na_feature_id = e.parent_id
EOSQL

  $queries{'test:unique_exon_parents'} = <<EOSQL;
SELECT count(distinct e.parent_id)
FROM DoTs.ExonFeature e,
     DoTS.GeneFeature g
WHERE g.row_alg_invocation_id  = $algInvId
AND g.na_feature_id = e.parent_id
EOSQL

  $queries{'test:aa_seqs'} = <<EOSQL;
SELECT count(distinct taf.aa_sequence_id)
FROM DoTs.Transcript t,
     DoTS.TranslatedAAFeature taf
WHERE t.row_alg_invocation_id  = $algInvId
AND taf.na_feature_id = t.na_feature_id
EOSQL

  $queries{'test:spliced_seqs'} = <<EOSQL;
SELECT count(distinct ss.na_sequence_id)
FROM DoTs.Transcript t,
     DoTS.SplicedNaSequence ss
WHERE t.row_alg_invocation_id  = $algInvId
AND t.na_sequence_id = ss.na_sequence_id
EOSQL

  $queries{'test:exon_rna_feature'} = <<EOSQL;
SELECT count(distinct rfe.exon_feature_id)
FROM DoTs.Transcript t,
     DoTS.RnaFeatureExon rfe
WHERE t.row_alg_invocation_id  = $algInvId
AND t.na_feature_id = rfe.rna_feature_id
EOSQL

  return \%queries;
}


sub _validateData{
  my ($queries, $answers, $dbh, $debug) = @_;
  my $passed = 0;
  my @failures;
  my @zeros;

  foreach my $queryName (keys %{$queries}){
    die "no answer provided for query '$queryName'" 
      unless defined($answers->{$queryName});
    my $stmt = $dbh->prepare($queries->{$queryName});
    my $time = time();
    print STDERR "running query '$queryName' ";
    $stmt->execute();
    my $result = $stmt->fetchrow_array();
    my $totTime = time() - $time + 1;
    print STDERR "($totTime sec)\n";

    my $expected = $answers->{$queryName};

    print STDERR "$queryName RESULT: $result\n" if $debug;
    print STDERR "$queryName EXPECTED: $expected\n" if $debug;

    if ($result != $expected) {
      push(@failures, [$queryName, $expected, $result]);
    } else {
      push (@zeros, $queryName) if ($expected == 0);
      $passed++;
    }
  }

  print "$passed tests succeeded\n";
  my $failed = scalar(@failures);
  print "$failed tests failed: \n";
  foreach my $failureInfo (@failures) {
    print "  $failureInfo->[0]   expected: $failureInfo->[1]    got: $failureInfo->[2]\n"
  }
  my $zero = scalar(@zeros);
  print "$zero expected 0: \n";
  foreach my $z (@zeros) {
    print "  $z\n"
  }
  print "\n";
}
