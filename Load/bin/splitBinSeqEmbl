#!/usr/bin/perl

## a script to split Embl Bin sequence into individual contig
## still under development

use strict;
use Getopt::Long;
use Bio::SeqFeature::Generic;
use ApiCommonData::Load::AnnotationUtils qw{getSeqIO};

my ($binFile, $tagName, $outputFilePrefix, $help);

&GetOptions('binFile=s' => \$binFile,
            'tagName=s' => \$tagName,
            'outputFilePrefix=s' => \$outputFilePrefix,
            'help|h' => \$help,
           );

&usage() if($help);
&usage("Missing a Required Argument") unless(defined $binFile && $tagName);

my $seq_in = Bio::SeqIO->new ('-file' => "<$binFile", '-format' => 'embl');

my $gapC = 0;

my ($organismName, $molType);

while (my $seq = $seq_in->next_seq() ) {
  my ($key, %subSeqs, %subFeats, %subOthers, $subStart, $subEnd);

  my $totalSeq = $seq->seq;

  my $segCnt = 1;
  my $preEnd = 0;

  foreach my $bioperlFeature ($seq->get_SeqFeatures()) {
    my $type = $bioperlFeature->primary_tag();

    if ($type eq "assembly_gap") {

      my ($gapType) = $bioperlFeature->get_tag_values('gap_type') if ($bioperlFeature->has_tag('gap_type'));
      if ($gapType eq "within scaffold") {  ## for assembly_gap within scaffold, not split but add to features
	my $nStart = $bioperlFeature->location->start - $preEnd;
	my $nEnd = $bioperlFeature->location->end - $preEnd;
	$bioperlFeature->location->start($nStart);
	$bioperlFeature->location->end($nEnd);
	push @{$subFeats{$segCnt}}, $bioperlFeature;
	next;
      }

      $subStart = $preEnd + 1;
      $subEnd = $bioperlFeature->location->start - 1;

      $subSeqs{$segCnt} = substr($totalSeq, $subStart-1, $subEnd - $subStart +1);

      $preEnd = $bioperlFeature->location->end;
      $segCnt++;
    } elsif ($type eq "CDS"
	    || $type eq "tRNA"
	    || $type eq "rRNA"
	    || $type eq "nRNA"
	    || $type eq "ncRNA"
	    ) {

      ## adjust start and end, and put th
      my @eachLocs = $bioperlFeature->location->each_Location();
      foreach my $eachLoc (@eachLocs) {
	my $eachNewStart = $eachLoc->start - $preEnd;
	if ($eachLoc->start_pos_type() eq "BEFORE" ) {  ## for partial gene
	  $eachNewStart = "<".$eachNewStart;
	}
	my $eachNewEnd = $eachLoc->end - $preEnd;
	if ($eachLoc->end_pos_type() eq "AFTER") {  ## for partial gene
	  $eachNewEnd = ">".$eachNewEnd;
	}
	$eachLoc->start($eachNewStart);
	$eachLoc->end($eachNewEnd);
      }

      push @{$subFeats{$segCnt}}, $bioperlFeature;

    } elsif ($type eq "source") {

      ($organismName) = $bioperlFeature->get_tag_values('organism') if ($bioperlFeature->has_tag('organism'));
      ($molType) = $bioperlFeature->get_tag_values('mol_type') if ($bioperlFeature->has_tag('mol_type'));

    } else {
    }
  }

  ## add the last piece of seq
  if (!$subSeqs{$segCnt}) {
    $subStart = $preEnd + 1;
    $subEnd = length ($totalSeq);
    $subSeqs{$segCnt} = substr($totalSeq, $subStart-1, $subEnd - $subStart +1);
  }

  foreach my $k (sort keys %subSeqs) {

    my $outputFile = ($outputFilePrefix) ? $outputFilePrefix : $binFile;
    $outputFile =~ s/.*\///;
    $outputFile =~ s/\.embl/\_/;
    $outputFile .= $k.".embl";

    my $seq_out = Bio::SeqIO->new('-file' => ">$outputFile", '-format' => 'embl');

    my $newSeqId = $outputFile;
    $newSeqId =~ s/\.embl$//;
    print STDERR "print $newSeqId...\n";
    my $newSeq = Bio::Seq->new(
			       -id => $newSeqId,
			       -accession_number => $newSeqId,
			       -seq => $subSeqs{$k},
			      );

    $newSeq->accession_number($newSeqId);

    my $sourceFeat = Bio::SeqFeature::Generic->new(
            -start      => 1,
            -end        => length($newSeq->seq),
            -primary    => 'source',
            -tag        => {
                organism => $organismName,
                mol_type => $molType,
			   },
						 );

    $newSeq->add_SeqFeature($sourceFeat);

    if ($subFeats{$k}) {
      foreach my $i (0..$#{$subFeats{$k}}) {
	$newSeq->add_SeqFeature($subFeats{$k}[$i]);
      }
    }

    $seq_out->write_seq($newSeq);

  }
}



sub usage {
  die
"
Usage: splitBinSeqEmbl --binFile ../fromProvider/PVL_00.embl --tagName 'assembly_gap' --outputFilePrefix 'PVL_00_'

where
  --binFile:  required, the bin file name and directory that wants to be split
  --tagName:  required, the tag name in the bin file that have contig info, eg. assembly_gap, contig, and etc.
  --outputFilePrefix:  optional, the prefix for the output files, eg. PVL_00_
";
}
